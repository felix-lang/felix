// Core types and type classes

typedef void = 0;
typedef unit = 1;
typedef bool = 2;
typedef any = any;

// -----------------------------------------------------------------------------

class IStream[f,t] {
  virtual gen read: f -> t;
}

class OStream[f,t] {
  virtual proc write: f * t;
}

class IOStream[f,t] {
  inherit IStream[f,t];
  inherit OStream[f,t];
}

// -----------------------------------------------------------------------------

class IFile[f,t] {
  inherit IStream [f,opt[t]];
}

class OFile[f,t] {
  inherit OStream[f,t];
  virtual proc close: f; // invalidates write operation
}

// -----------------------------------------------------------------------------

class IBuffer [f] {
  virtual gen read: f -> address * int -> int * bool;
}

class OBuffer [f] {
  virtual gen write : f -> address * int -> int * bool;
}

// -----------------------------------------------------------------------------

// note: eq is not necessarily required for a membership test
// for example: string member of regexp doesn't require
// string equality
// Set need not be finite (example regexp again)
// A list is a set, despite the duplications
class Set[c,t] {
  fun mem (elt:t, container:c):bool => elt \in container;
  virtual fun \in : t * c-> bool;
}

// roughly, a finite Set
class Container [c,v]
{
  inherit Set[c,v];
  virtual fun len: c -> size;
  virtual fun empty(x: c): bool => len x == size(0);
}

// -----------------------------------------------------------------------------

class Monad [M: TYPE->TYPE] {
  virtual fun bind[a,b]: M a * (a -> M b) -> M b;
  virtual fun ret[a]: a -> M a;
}

// -----------------------------------------------------------------------------

class Str [T] {
  virtual fun str: T -> string;
}

class Repr [T with Str[T]] {
  virtual fun repr (t:T) : string => str t;
}

class Show [T] {
  inherit Str[T];
  inherit Repr[T];
}

// -----------------------------------------------------------------------------

instance Str[void] {
  fun str (x:void) => "void";
}
open Show[void];

// -----------------------------------------------------------------------------

instance Str[unit] {
  fun str (x:unit) => "()";
}
open Show[unit];

instance Eq[unit] {
  fun == (x:unit, y:unit) => true;
}
open Eq[unit];

