class FlxCache
{
fun past_time () => -1.0;
fun future_time () => double(ulong(-1)); // a hacky way to get a big number

fun gramtime(debugln: string -> 0) (path:string, s:string) : double = {
  //println$ "Path=" + path + " file = " + s;
  fun maxtime (x:double) (s:string) => max (x, gramtime debugln (path, s));
  if s.[0]=="@".char do
    var file = Filename::join$ path, s.[1 to].strip;
    var filetime = dfiletime(file,0.0);
    if filetime == 0.0 do
      println$ "Grammar include file " + file " doesn't exist, exiting";
      System::exit 1;
    done
    debugln$ "Grammar include file " + file + " time=" + strfiletime(filetime);
    var filetext = load file;
    var files = split (filetext, "\n");
    files = map strip of (string) files;
    files = filter (fun (s:string) => s != "") files;
    files = map (fun (s:string) => Filename::join (split(s,"/"))) files;
    //println$ "Files=" + files;
    return fold_left maxtime filetime files;
  else
    file = Filename::join$ path, s;
    filetime = dfiletime(file,0.0);
    if filetime == 0.0 do
      println$ "Grammar file " + file " doesn't exist, exiting";
      System::exit 1;
    done
    debugln$ "Grammar file " + file + " time=" + strfiletime(filetime);
    return filetime;
  done
}


fun strfiletime0 (x:double) = {
  return
    if x == #past_time then "BIG BANG"
    elif x == #future_time then "BIG CRUNCH"
    else fmt (x, fixed (0,3))
    endif
  ;
}

fun strfiletime (x:double) = {
  assert x != 0.0;
  return strfiletime0 x;
}

fun dfiletime(var f:string, dflt:double)=
{
  var x = FileStat::filetime (f);
  x = if x == 0.0 then dflt else x endif;
  //debugln$ "Time of file '" + f + "' is " + strfiletime x; 
  return x;
}

fun maxf (x: double) (f:string) =>
  if f == "" then x else max (x, dfiletime (f,#past_time))
;

fun cal_files_time (fs: list[string])=> fold_left maxf #past_time fs;

fun cal_dep_time(debugln:string -> 0, OUTPUT_DIR:string, f:string)= {
  fun cal_time_from_dep (df: string):double=
  {
    val deptext = load df;
    val lines = split (deptext, "\n"); 
    debugln$ "Deps=" + str(lines);
    val deptime = cal_files_time lines;
    debugln$ "Deptime=" + strfiletime(deptime);
    return deptime;
  }

  val fdoctime = dfiletime(f+".fdoc",#past_time);
  val flxtime = dfiletime(f+".flx",#past_time);
  val depfilename = cache_join (OUTPUT_DIR, f+".dep");
  debugln$ "Dependency file name = " + depfilename;
  val deptime = cal_time_from_dep (depfilename);
  debugln$ "fdoc time = " + strfiletime fdoctime;
  debugln$ "flx time = " + strfiletime flxtime;
  debugln$ "dep time = " + strfiletime deptime;
  val srctime =  max( fdoctime, max (flxtime, deptime));
  debugln$ "Source time = " + strfiletime srctime;
  return srctime;
}

// FLX_INSTALL_DIR: root for finding standard grammar
// STDGRAMMAR: root standard grammar key, within FLX_INSTALL_DIR
//      usually "@grammar/grammar.files"
// FLXG: absolute filename of felix compiler executable

// CACHE_DIR: absolute filename of binary cache
// OUTPUT_DIR: absolute filename of text cache

// DEFAULT_CACHE_DIR: default location of CACHE_DIR
// DEFAULT_OUTPUT_DIR: default location of OUTPUT_DIR
//    These defaults are used to determine if the
//    the cache should be deleted automatically
//    or a an interactive query used to verify.
//    Automatic deletion requies the caches to be the default.
// CLEAR_CACHE: switch to force clearing the cache

typedef cache_validation_spec_t = 
(
   FLX_INSTALL_DIR:string, 
   FLX_TARGET_SUBDIR:string,
   STDGRAMMAR:string, 
   FLXG:string,    
   CACHE_DIR:string,
   OUTPUT_DIR:string,
   DEFAULT_CACHE_DIR: string,
   DEFAULT_OUTPUT_DIR: string,
   CLEAR_CACHE: int,
   debugln : string -> 0,
   xqt: string -> string,
   quote: string -> string
);


// returns cache time
gen validate_cache  (spec: cache_validation_spec_t) : int * double =
{

  Directory::mkdirs(spec.CACHE_DIR);

  var flxg_time = dfiletime(spec.FLXG, #future_time);
  spec.debugln$ "Flxg_time=" + strfiletime(flxg_time);

  var flxg_stamp = Filename::join spec.CACHE_DIR "flxg_time.stamp";
  var cache_time = dfiletime(flxg_stamp,#future_time);
  spec.debugln$ "cache_time=" + strfiletime(cache_time);

  var flxg_stamp_data = load flxg_stamp;
  //println$ "Flxg_stamp_data=" + flxg_stamp_data; 

  var flxg_stamp_time = match flxg_stamp_data.atof with | 0.0 => #past_time | ?x => x;

  spec.debugln$ "Flxg_stamp_data : " + strfiletime(flxg_stamp_time);
  var grammar_time = gramtime spec.debugln (Filename::join (spec.FLX_INSTALL_DIR,"share","lib"), spec.STDGRAMMAR);
  spec.debugln$ "Grammar text time=" + strfiletime (grammar_time);
  var automaton_name = 
    cache_join(spec.CACHE_DIR, 
    Directory::mk_absolute_filename (Filename::join (list( 
      spec.FLX_INSTALL_DIR,
      spec.FLX_TARGET_SUBDIR,
      "lib",
      "grammar",
      "syntax.automaton"
    ))))
  ;

  var automaton_time = dfiletime(automaton_name,#future_time);
  spec.debugln$ "Automaton " + automaton_name + " time=" + strfiletime(automaton_time);

  // Still if this fails,  the compiler is new and 
  // the cache should be deleted.
  if cache_time != #future_time and flxg_stamp_time != flxg_time do
    println$ "Cache may be out of date due to compiler upgrade!";
    println$ "Flxg compiler time stamp=" + strfiletime(flxg_time);
    println$ "Cache time stamp        =" + strfiletime(cache_time);
    if not (
      (spec.OUTPUT_DIR == "/" or spec.OUTPUT_DIR == "" or spec.OUTPUT_DIR == ".") or
      (spec.CACHE_DIR == "/" or spec.CACHE_DIR == "" or spec.CACHE_DIR == ".")
    )
    do 
      spec.CLEAR_CACHE = 1;
    done
  elif automaton_time != #future_time and grammar_time > automaton_time do
    println$ "Cache may be out of date due to grammar upgrade!";
    println$ "Grammar time stamp          =" + strfiletime(grammar_time);
    println$ "Automaton.syntax time stamp =" + strfiletime(automaton_time);
    if not (
      (spec.OUTPUT_DIR == "/" or spec.OUTPUT_DIR == "" or spec.OUTPUT_DIR == ".") or
      (spec.CACHE_DIR == "/" or spec.CACHE_DIR == "" or spec.CACHE_DIR == ".")
    )
    do 
      spec.CLEAR_CACHE = 1;
    done
  done

  // FFF BE CAREFUL! The value "/" for these caches is perfectly good
  if spec.CLEAR_CACHE != 0 do
    // refuse to delete "" or "/" or ".", basic safety check
    if 
      (spec.OUTPUT_DIR == "/" or spec.OUTPUT_DIR == "" or spec.OUTPUT_DIR == ".") or
      (spec.CACHE_DIR == "/" or spec.CACHE_DIR == "" or spec.CACHE_DIR == ".")
    do
      println "WILL NOT DELETE CACHES";
      println$ "output cache " + spec.OUTPUT_DIR;
      println$ "binary cache " + spec.CACHE_DIR;
      System::exit(1);
    done

    // if the cache is default $HOME one, which cannot be ""
    // due to previous check, then delete without question
    if 
      spec.OUTPUT_DIR == spec.DEFAULT_OUTPUT_DIR and 
      spec.CACHE_DIR == spec.DEFAULT_CACHE_DIR 
    do 
      var answer = "Y\n";
    else
      // otherwise prompt for confirmation
      println$ "Would delete cache " + spec.OUTPUT_DIR;
      println$ "Would delete cache " + spec.CACHE_DIR;
      print "Are you SURE? (Y/n) "; fflush stdout;
      answer = readln stdin;
    done
    if answer == "Y\n" or answer == "y\n" do
      // delete the caches loudly
      println$ "Delete cache " + spec.OUTPUT_DIR;
      if PLAT_WIN32 do
          C_hack::ignore$ spec.xqt("rd /Q /S " + spec.quote(spec.OUTPUT_DIR));
      else
          C_hack::ignore$ spec.xqt("rm -rf " + spec.quote(spec.OUTPUT_DIR));
      done
      println$ "Delete cache " + spec.CACHE_DIR;

      if PLAT_WIN32 do
          C_hack::ignore$ spec.xqt("rd /Q /S " + spec.quote(spec.CACHE_DIR));
      else
          C_hack::ignore$ spec.xqt("rm -rf " + spec.quote(spec.CACHE_DIR));
      done

      Directory::mkdirs(spec.CACHE_DIR);
      var f = fopen_output flxg_stamp;
      write(f, fmt(flxg_time, fixed (0,3)));
      f.fclose;
    else
      // otherwise exit with an error
      println "Not deleting cache, exiting with error 1";
      System::exit(1);
    done
  done
  return spec.CLEAR_CACHE, cache_time;
}

fun cache_join (c:string, var f:string) = 
{
  //debugln$ "[cache_join] " + c + " with  " + f;
  if PLAT_WIN32 do
    if f.[1 to 3] == ":\\" do f = f.[0 to 1]+f.[2 to]; 
    elif f.[1] == char ":" do f = f.[0 to 1]+"\\"+f.[2 to]; 
    done
    if f.[0] == char "\\" do f = f.[1 to]; done
  else
    if f.[0] == char "/" do f = f.[1 to]; done
  done
    var k = Filename::join(c,f);
    //debugln$ "[cache_join] result = " + k;
    return k;
}

}
